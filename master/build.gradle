plugins {
    id 'org.springframework.boot' version '2.3.2.RELEASE'
}

ext {
    defaultEncoding = 'UTF-8'
}

allprojects {
    apply plugin: 'eclipse'

    eclipse.project {
        buildCommand 'org.eclipse.buildship.core.gradleprojectbuilder'
        natures 'org.eclipse.buildship.core.gradleprojectnature'
    }

    // task cleanEclipse
    tasks.cleanEclipse.doLast {
        project.delete('.settings')
    }

    // task eclipse
    tasks.eclipse.doLast {
        def settingsDir =   new File('.settings')
        settingsDir.mkdirs()
    File buildship_prefs = file(".settings/org.eclipse.buildship.core.prefs")
        buildship_prefs.append('''
            connection.project.dir=
            eclipse.preferences.version=1
            '''. stripIndent())
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    // apply plugin: 'eclipse-wtp'
    // apply plugin: 'checkstyle'
    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    repositories {
        jcenter()
        mavenCentral()
    }

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    tasks.withType(AbstractCompile)*.options*.encoding = defaultEncoding
    tasks.withType(GroovyCompile)*.groovyOptions*.encoding = defaultEncoding

    tasks.withType(Test) {
        testLogging {
            exceptionFormat = 'full'
            showCauses true
            showExceptions true
            showStackTraces true
            showStandardStreams true
        }
    }

    dependencies {
        implementation 'org.projectlombok:lombok:1.18.12'
        testImplementation 'org.projectlombok:lombok:1.18.12'
        testImplementation 'junit:junit:4.13'
    }
}
